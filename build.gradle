plugins {
    id 'java'
    id 'com.github.johnrengelman.shadow' version '7.1.2'
    id 'signing'
    id 'maven-publish'
}

group 'de.heisluft.reveng'
version '0.0.3'

sourceCompatibility = targetCompatibility = JavaVersion.VERSION_1_8

task sourcesJar(type: Jar, dependsOn: classes) {
    archiveClassifier.set 'sources'
    from sourceSets.main.allSource
}

repositories {
    mavenCentral()
    maven {
        url 'https://heisluft.de/maven/'
    }
}

dependencies {
    implementation 'org.ow2.asm:asm-tree:9.2'
    implementation 'de.heisluft:futile:1.1.0'
}

shadowJar.manifest.attributes.put('Main-Class', 'de.heisluft.reveng.Remapper')

publishing {
    repositories.maven {
        url mavenUrl
        credentials {
            username = mavenUsername
            password = mavenPassword
        }
        authentication.withType(BasicAuthentication)
    }
    publications.create('maven', MavenPublication) {
        it.from components.getByName('java')
        it.artifact sourcesJar
    }
}

signing {
    useGpgCmd()
    sign publishing.publications.maven
}