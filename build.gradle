plugins {
    id 'java'
    id 'com.gradleup.shadow' version '8.3.0'
    id 'signing'
    id 'maven-publish'
}

group = 'de.heisluft.deobf.tooling'
version = '0.2.0'

java.toolchain.languageVersion.set(JavaLanguageVersion.of(21))

tasks.register('sourcesJar', Jar.class) {
    dependsOn(classes)
    archiveClassifier.set 'sources'
    from sourceSets.main.allSource
}

tasks.withType(JavaCompile.class).configureEach {
    options.encoding = 'UTF-8'
}

repositories {
    mavenCentral()
    maven {
        url 'https://heisluft.de/maven/'
    }
}

dependencies {
    implementation 'org.ow2.asm:asm-tree:9.7.1'
    implementation 'de.heisluft:futile:1.1.0'
    implementation 'de.heisluft.cli:simple-options:0.3.1'
    implementation project("mappings")
}

shadowJar.manifest.attributes.put('Main-Class', 'de.heisluft.deobf.tooling.Remapper')

publishing {
    repositories.maven {
        url = mavenUrl
        credentials {
            username = mavenUsername
            password = mavenPassword
        }
        authentication.withType(BasicAuthentication)
    }
    publications.create('maven', MavenPublication) {
        it.from components.getByName('java')
        it.artifact sourcesJar
    }
}

signing {
    useGpgCmd()
    sign publishing.publications.maven
}