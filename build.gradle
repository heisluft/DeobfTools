plugins {
    id 'java'
    id 'com.github.johnrengelman.shadow' version '7.1.2'
    id 'signing'
    id 'maven-publish'
}

group 'de.heisluft.deobf.tooling'
version '0.1.7'

java.toolchain.languageVersion.set(JavaLanguageVersion.of(8))

tasks.register('sourcesJar', Jar.class) {
    dependsOn(classes)
    archiveClassifier.set 'sources'
    from sourceSets.main.allSource
}

tasks.withType(JavaCompile.class).configureEach {
    options.encoding = 'UTF-8'
}

repositories {
    mavenCentral()
    maven {
        url 'https://heisluft.de/maven/'
    }
}

dependencies {
    implementation 'org.ow2.asm:asm-tree:9.6'
    implementation 'de.heisluft:futile:1.1.0'
    implementation project("mappings")
}

shadowJar.manifest.attributes.put('Main-Class', 'de.heisluft.deobf.tooling.Remapper')

publishing {
    repositories.maven {
        url mavenUrl
        credentials {
            username = mavenUsername
            password = mavenPassword
        }
        authentication.withType(BasicAuthentication)
    }
    publications.create('maven', MavenPublication) {
        it.from components.getByName('java')
        it.artifact sourcesJar
    }
}

signing {
    useGpgCmd()
    sign publishing.publications.maven
}